
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастройкаФормы();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФормы()
	Элементы.СтраницаЧекЛист.Видимость = Объект.НуженЧекЛист;
КонецПроцедуры

&НаКлиенте
Процедура НуженЧекЛистПриИзменении(Элемент)
	Если Объект.ЧекЛист.Количество() > 0 Тогда
		Объект.НуженЧекЛист = Истина;
	КонецЕсли;
	НастройкаФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Объект.Ответственный) Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Статус) Тогда
		Если ЗначениеЗаполнено(Объект.Ответственный) Тогда
			Объект.Статус = Перечисления.СтатусыЗадач.НазначенОтветственный;
		Иначе
			Объект.Статус = Перечисления.СтатусыЗадач.НеОбработан;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриИзменении(Элемент)
	ПересчетСуммы();
КонецПроцедуры

&НаКлиенте
Процедура ПересчетСуммы()
	Если НЕ Объект.Сумма = Объект.Работы.Итог("Сумма") Тогда
		Объект.Сумма = Объект.Работы.Итог("Сумма");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПересчетСуммыСтроки()
	СтрокаТабличнойЧасти = Элементы.Работы.ТекущиеДанные;
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	ПересчетСуммы();
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоличествоПриИзменении(Элемент)
	ПересчетСуммыСтроки();
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПриИзменении(Элемент)
	ПересчетСуммыСтроки();	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Работы.ТекущиеДанные;
	СтрокаТабличнойЧасти.Цена = УстановитьЦену(СтрокаТабличнойЧасти.Номенклатура);
	ПересчетСуммыСтроки();
КонецПроцедуры

&НаСервере
Функция УстановитьЦену(Номенклатура)
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&ДатаДок,
	|			Номенклатура = &Номенклатура
	|				И Партнер = &Партнер) КАК ЦеныНоменклатурыСрезПоследних");
	Запрос.УстановитьПараметр("Партнер",Объект.Партнер);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("ДатаДок",Объект.Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Цена;
	КонецЕсли;
	
	Возврат 0;
КонецФункции
